execute if score #fathoms.copy_fish fathoms.dummy matches 1 if entity @s[tag=fathoms.block.chopping_block.has_fish] run return run function fathoms:block/chopping_block/interaction/copy_fish
execute if data storage fathoms:temp root.item{id:"minecraft:honeycomb"} if entity @s[tag=fathoms.block.chopping_block.has_fish,tag=!fathoms.block.chopping_block.waxed] run return run function fathoms:block/chopping_block/interaction/set_state/waxed
execute if entity @s[tag=fathoms.block.chopping_block.waxed] run return run function fathoms:block/chopping_block/interaction/set_state/fail/waxed
execute if entity @s[tag=fathoms.block.chopping_block.tough_shell] if score #fathoms.should_cut_fish fathoms.dummy matches 1.. unless predicate fathoms:value_check/chopping_block/tough_shell_piercable run return run function fathoms:block/chopping_block/interaction/set_state/fail/tough_shell
execute unless score #fathoms.has_changed_state fathoms.dummy matches 1 if score #fathoms.should_cut_fish fathoms.dummy matches 1.. if entity @s[tag=fathoms.block.chopping_block.has_fish] run function fathoms:block/chopping_block/interaction/cut_fish/start
execute unless score #fathoms.has_changed_state fathoms.dummy matches 1 if entity @s[tag=fathoms.block.chopping_block.has_fish] run function fathoms:block/chopping_block/interaction/set_state/empty
execute unless score #fathoms.has_changed_state fathoms.dummy matches 1 if data storage fathoms:temp root.item.components."minecraft:custom_data".fathoms.fish.chopping_block_loot_table if entity @s[tag=fathoms.block.chopping_block.empty] run function fathoms:block/chopping_block/interaction/set_state/has_fish with storage fathoms:temp root.item.components."minecraft:custom_data".fathoms